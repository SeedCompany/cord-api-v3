module default {
  abstract type Project extending
    Mixin::Postable,
    Comments::Aware,
    Resource,
    Project::ContextAware,
    Mixin::Named,
    Mixin::Pinnable,
    Mixin::Taggable
  {
    type := <Project::Type>.__type__.name[9:-7];

    overloaded name {
      constraint exclusive;
    };

    overloaded required ownSensitivity: Sensitivity {
      annotation description := "The sensitivity of the project. \
        This is user settable for internships and calculated for translation projects";
      default := Sensitivity.High;
    };
    
    departmentId: str {
      constraint exclusive;
      constraint expression on (<int32>__subject__ > 0 and len(__subject__) = 5);
      rewrite insert, update using (
        if (
          not exists .departmentId and
          .status <= Project::Status.Active and
          .step >= Project::Step.PendingFinanceConfirmation
        ) then ((
          with block := (
            if __subject__ is MultiplicationTranslationProject then ((
              with primaryPartnership := assert_exists(
                __subject__.primaryPartnership,
                message := "Project must have a primary partnership"
              )
              select assert_exists(
                primaryPartnership.partner.departmentIdBlock,
                message := "Available Department IDs have not been declared"
              )
            )) else (
              assert_exists(
                assert_exists(
                  __subject__.primaryLocation,
                  message := "Project must have a primary location"
                ).fundingAccount,
                message := "Project's primary location must have a funding account"
              ).departmentIdBlock
            )
          )
          select assert_exists(block.nextAvailable, message := "No department ID is available")
        )) else .departmentId
      );
    };

    required step: Project::Step {
      default := Project::Step.EarlyConversations;
    };
    status := Project::statusFromStep(.step);
    latestWorkflowEvent := (select .workflowEvents order by .at desc limit 1);
    workflowEvents := .<project[is Project::WorkflowEvent];
    trigger assertMatchingLatestWorkflowEvent after insert, update for each do (
      assert(
        __new__.latestWorkflowEvent.to ?= __new__.step
        or (not exists __new__.latestWorkflowEvent and __new__.step = Project::Step.EarlyConversations),
        message := "Project step must match the latest workflow event"
      )
    );

    mouStart: cal::local_date;
    mouEnd: cal::local_date;
    constraint expression on (.mouEnd >= .mouStart);
    initialMouEnd: cal::local_date {
      default := .mouEnd;
      rewrite update using (.mouEnd if .status = Project::Status.InDevelopment else .initialMouEnd);
    }
    
    estimatedSubmission: cal::local_date;
    
    financialReportReceivedAt: datetime;
    financialReportPeriod: ReportPeriod;
    
    multi link members := .<project[is Project::Member];
    single link membership := (select .members filter .user = global default::currentUser limit 1);
    
#     multi link engagements := .<project[is Engagement];
    property engagementTotal := count(.<project[is Engagement]);
    
    primaryLocation: Location;
    trigger enforceFundingAccount after update for each do (
      assert(
        any(__new__.primaryLocation.fundingAccount.accountNumber > 0)
          or not exists __new__.primaryLocation, # allow clearing
        message := "Project must have a primary location with a specified funding account"
      )
    );
    marketingLocation: Location;
    multi otherLocations: Location;
    fieldRegion: FieldRegion;
    marketingRegionOverride: FieldRegion;
    
    link rootDirectory: Directory;
    
    partnerships := .<project[is Partnership];
    primaryPartnership := (
      # avoiding backlink here because of partial path resolution bug
      # https://github.com/geldata/gel/issues/8633
      select Partnership
      filter Partnership.primary and Partnership.project = __source__
      limit 1
    );
    
    overloaded link projectContext: Project::Context {
      default := (insert Project::Context {
        # https://github.com/geldata/gel/issues/3960
        # projects := {__subject__},
      });
      on source delete delete target;
    }
    
    trigger createBudgetOnInsert after insert for each do (
      insert default::Budget {
        createdAt := datetime_of_transaction(),
        modifiedAt := datetime_of_transaction(),
        createdBy := assert_exists(global currentActor),
        modifiedBy := assert_exists(global currentActor),
        project := __new__,
        projectContext := __new__.projectContext,
      }
    );

    trigger createPeriodicReports after insert for each do (
      with
        interval := (select 
          if __new__.financialReportPeriod = default::ReportPeriod.Monthly then '1' else '3'),
        reportRanges := Project::create_periodic_report_ranges(
          __new__.mouStart,
          __new__.mouEnd,
          interval
        )
      for reportRange in reportRanges 
      union (
        (insert default::FinancialReport {
          createdAt := datetime_of_statement(),
          modifiedAt := datetime_of_statement(),
          createdBy := assert_exists(global currentActor),
          modifiedBy := assert_exists(global currentActor),
          project := __new__,
          projectContext := __new__.projectContext,
          container := __new__,
          period := reportRange
        }),
        (insert default::NarrativeReport {
          createdAt := datetime_of_statement(),
          modifiedAt := datetime_of_statement(),
          createdBy := assert_exists(global currentActor),
          modifiedBy := assert_exists(global currentActor),
          project := __new__,
          projectContext := __new__.projectContext,
          container := __new__,
          period := reportRange
        })
      )
    );

    trigger addRemovePeriodicReports after update for each 
      when (
        __old__.mouStart ?!= __new__.mouStart
        or __old__.mouEnd ?!= __new__.mouEnd
        or __old__.financialReportPeriod ?!= __new__.financialReportPeriod
      ) 
      do (
      with
        newMouStart := __new__.mouStart,
        oldMouStart := __old__.mouStart,
        newMouEnd := __new__.mouEnd,
        oldMouEnd := __old__.mouEnd,
        existingReports := (
          select FinancialReport union NarrativeReport
          filter .container.id = __old__.id
        ),
        interval := (
          select (if __new__.financialReportPeriod = default::ReportPeriod.Monthly then '1' else '3')
        ),
        requestedReportPeriods := Project::create_periodic_report_ranges(
          __new__.mouStart,
          __new__.mouEnd,
          interval
        ),
        reportsForDeletion := (
            select existingReports
            filter not exists .reportFile
        )  
      select if not exists __new__.mouStart or not exists __new__.mouEnd then (     
        for report in reportsForDeletion 
        union (
          delete report 
        )
      )
      else if __old__.financialReportPeriod ?!= __new__.financialReportPeriod
        or (__new__.mouStart > __old__.mouStart) ?? false 
        or (__new__.mouEnd < __old__.mouEnd) ?? false then (
        with
          requestedReportPeriodsForInsertion := (
            select requestedReportPeriods
            filter requestedReportPeriods not in existingReports.period
          ),
          financialReports := (for reportPeriod in requestedReportPeriodsForInsertion
          union (
            insert default::FinancialReport {
              createdAt := datetime_of_statement(),
              modifiedAt := datetime_of_statement(),
              createdBy := assert_exists(global currentActor),
              modifiedBy := assert_exists(global currentActor),
              project := __new__,
              projectContext := __new__.projectContext,
              container := __new__,
              period := reportPeriod,
            }
          )),
          narrativeReports := (for reportPeriod in requestedReportPeriodsForInsertion
          union (
            insert default::NarrativeReport {
              createdAt := datetime_of_statement(),
              modifiedAt := datetime_of_statement(),
              createdBy := assert_exists(global currentActor),
              modifiedBy := assert_exists(global currentActor),
              project := __new__,
              projectContext := __new__.projectContext,
              container := __new__,
              period := reportPeriod,
            }
          ))
        for report in reportsForDeletion 
        union (
          delete report
          filter report.period not in requestedReportPeriods
        )  
      ) 
      else if newMouStart ?!= oldMouStart 
        or newMouEnd ?!= oldMouEnd then (
        with
          requestedReportPeriodsForInsertion := (
            select requestedReportPeriods
            filter requestedReportPeriods not in existingReports.period
          ),
          financialReports := (for reportPeriod in requestedReportPeriodsForInsertion
          union (
            insert default::FinancialReport {
              createdAt := datetime_of_statement(),
              modifiedAt := datetime_of_statement(),
              createdBy := assert_exists(global currentActor),
              modifiedBy := assert_exists(global currentActor),
              project := __new__,
              projectContext := __new__.projectContext,
              container := __new__,
              period := reportPeriod,
            }
          )),
          narrativeReports := (for reportPeriod in requestedReportPeriodsForInsertion
          union (
            insert default::NarrativeReport {
              createdAt := datetime_of_statement(),
              modifiedAt := datetime_of_statement(),
              createdBy := assert_exists(global currentActor),
              modifiedBy := assert_exists(global currentActor),
              project := __new__,
              projectContext := __new__.projectContext,
              container := __new__,
              period := reportPeriod,
            }
          ))
        select financialReports union narrativeReports
      ) else (
        select <PeriodicReport>{}
      )
    );
  }
  
  abstract type TranslationProject extending Project {
    multi link engagements := .<project[is LanguageEngagement];
    multi link languages := .engagements.language;
    
    trigger confirmProjectSens after update for each do (
      assert(
        __new__.ownSensitivity = max(__new__.languages.ownSensitivity) ?? Sensitivity.High,
        message := "TranslationProject sensitivity is automatically set to \
          (and required to be) the highest sensitivity Language engaged"
      )
    );
  }

  type MomentumTranslationProject extending TranslationProject;
  type MultiplicationTranslationProject extending TranslationProject;
  
  type InternshipProject extending Project {
    multi link engagements := .<project[is InternshipEngagement];
  }
}
 
module Project {
  abstract type Child extending default::Resource, ContextAware {
    annotation description := "\
      A type that is a child of a project. \
      It will always have a reference to a single project that it is under.";
    
    required project: default::Project {
      readonly := true;
      on target delete delete source;
    };
    
    trigger enforceCorrectProjectContext after insert, update for each do (
      assert(
        __new__.projectContext = __new__.project.projectContext,
        message := "Given project context must match given project's context"
      )
    );
  }
  
  abstract type ContextAware {
    annotation description := "\
      A type that has a project context, which allows it to be
      aware of the sensitivity & current user membership for the associated context.";
    
    required projectContext: Context {
      on target delete delete source;
    }
    index on (.projectContext);
    
    optional ownSensitivity: default::Sensitivity {
      annotation description := "\
        A writable source of a sensitivity. \
        This doesn't necessarily mean it be the same as .sensitivity, which is what is used for authorization.";
    };
    
    required single property sensitivity :=
      max(.projectContext.projects.ownSensitivity)
      ?? (.ownSensitivity ?? default::Sensitivity.High);
    required single property isMember := exists .projectContext.projects.membership;
  }
  
  scalar type Type extending enum<
    MomentumTranslation,
    MultiplicationTranslation,
    Internship
  >;

  type FinancialApprover {
    required user: default::User {
      constraint exclusive;
    };
    required multi projectTypes: Type;
  }
  
  type Context {
    annotation description := "\
      A type that holds a reference to a list of projects. \
      This allows multiple objects to hold a reference to the same list. \
      For example, Language & Ethnologue::Language share the same context / project list.";
    
    multi projects: default::Project {
      on target delete allow;
    };
  }

  # creates the ranges for the given start and end dates based upon the given month interval
  function create_periodic_report_ranges(startDate: cal::local_date, endDate: cal::local_date, 
    monthInterval: str) -> set of range<cal::local_date>
    using (
      with
        reportingPeriod := range(<cal::local_date>startDate, <cal::local_date>endDate),
        reportPeriodStartDates := range_unpack(reportingPeriod, <cal::date_duration>(monthInterval ++ ' month')),
        reportPeriodRanges := (
          for firstDayOfMonth in reportPeriodStartDates
          union (
            with
              firstDayOfNextMonth := (select firstDayOfMonth + <cal::relative_duration>(monthInterval ++ ' month')),
              lastDayOfMonth := firstDayOfNextMonth - <cal::relative_duration>'1 day'
            select range(<cal::local_date>firstDayOfMonth, <cal::local_date>lastDayOfMonth)
          ))
      select reportPeriodRanges
  );

  # function insertReports(requestedReportPeriods: set of range<cal::local_date>,
  #   existingReports: array<PeriodicReport>, newProject: default::Project) -> optional str
  #   using (
  #     with
  #       requestedReportPeriodsForInsertion := (
  #         select requestedReportPeriods
  #         filter requestedReportPeriods not in existingReports.period
  #       ),
  #       financialReports := (for reportPeriod in requestedReportPeriodsForInsertion
  #       union (
  #         insert default::FinancialReport {
  #           createdAt := datetime_of_statement(),
  #           modifiedAt := datetime_of_statement(),
  #           createdBy := assert_exists(global default::currentActor),
  #           modifiedBy := assert_exists(global default::currentActor),
  #           project := __new__,
  #           projectContext := __new__.projectContext,
  #           container := __new__,
  #           period := reportPeriod,
  #         }
  #       )),
  #       narrativeReports := (for reportPeriod in requestedReportPeriodsForInsertion
  #       union (
  #         insert default::NarrativeReport {
  #           createdAt := datetime_of_statement(),
  #           modifiedAt := datetime_of_statement(),
  #           createdBy := assert_exists(global default::currentActor),
  #           modifiedBy := assert_exists(global default::currentActor),
  #           project := __new__,
  #           projectContext := __new__.projectContext,
  #           container := __new__,
  #           period := reportPeriod,
  #         }
  #       ))
  #     select financialReports union narrativeReports
  #   )
}
