module default {
  abstract type Actor {
    multi roles: Role;
  }

  type User extending Comments::Aware, Resource, Actor, Mixin::Pinnable {
    email: str {
      constraint exclusive;
    };
    required realFirstName: str;
    required realLastName: str;
    required displayFirstName: str {
      default := .realFirstName;
    };
    required displayLastName: str {
      default := .realLastName;
    };
    phone: str;
    required timezone: str {
      default := 'America/Chicago';
    };
    about: str;
    required status: User::Status {
      default := User::Status.Active;
    };
    title: str;
    multi link pins: Mixin::Pinnable {
      on target delete allow;
    }
    multi link education: User::Education {
      on target delete allow;
      on source delete delete target;
    }
    multi link unavailabilities: User::Unavailability {
      on target delete allow;
      on source delete delete target;
    }
    multi locations: Location;

    access policy CanSelectUpdateReadGeneratedFromAppPoliciesForUser
    allow select, update read using (
      (
        exists (<Role>{'Consultant', 'ConsultantManager', 'FieldPartner', 'FinancialAnalyst', 'LeadFinancialAnalyst', 'Controller', 'Marketing', 'Fundraising', 'ExperienceOperations', 'ProjectManager', 'RegionalDirector', 'FieldOperationsDirector', 'StaffMember'} intersect global currentRoles)
        or .id ?= global currentActorId
        or (
          exists (<Role>{'Intern', 'Mentor'} intersect global currentRoles)
          and exists { "Stubbed .isMember for User/Unavailability" }
        )
      )
    );

    access policy CanInsertGeneratedFromAppPoliciesForUser
    allow insert using (
      exists (<Role>{'Consultant', 'ConsultantManager', 'FinancialAnalyst', 'LeadFinancialAnalyst', 'Controller', 'ProjectManager', 'RegionalDirector', 'FieldOperationsDirector'} intersect global currentRoles)
    );
  }

  type SystemAgent extending Actor, Mixin::Named {
    overloaded name { constraint exclusive };
  }
}
 
module User {
  type Education extending default::Resource {
    required degree: Degree;
    required major: str;
    required institution: str;

    access policy CanSelectUpdateReadGeneratedFromAppPoliciesForEducation
    allow select, update read using (
      exists (<default::Role>{'ConsultantManager', 'FieldOperationsDirector', 'FinancialAnalyst', 'LeadFinancialAnalyst', 'Controller', 'Marketing', 'Fundraising', 'ExperienceOperations', 'ProjectManager', 'RegionalDirector', 'StaffMember'} intersect global default::currentRoles)
    );

    access policy CanInsertGeneratedFromAppPoliciesForEducation
    allow insert using (
      exists (<default::Role>{'ProjectManager', 'RegionalDirector', 'FieldOperationsDirector'} intersect global default::currentRoles)
    );
  }
  
  type Unavailability extending default::Resource {
    required description: str;
    required dates: range<datetime>;
    `start` := assert_exists(range_get_lower(.dates));
    `end` := assert_exists(range_get_upper(.dates));

    access policy CanSelectUpdateReadGeneratedFromAppPoliciesForUnavailability
    allow select, update read using (
      (
        exists (<default::Role>{'Consultant', 'ConsultantManager', 'FieldPartner', 'FinancialAnalyst', 'LeadFinancialAnalyst', 'Controller', 'Marketing', 'Fundraising', 'ExperienceOperations', 'ProjectManager', 'RegionalDirector', 'FieldOperationsDirector', 'StaffMember'} intersect global default::currentRoles)
        or (
          exists (<default::Role>{'Intern', 'Mentor'} intersect global default::currentRoles)
          and exists { "Stubbed .isMember for User/Unavailability" }
        )
      )
    );

    access policy CanInsertGeneratedFromAppPoliciesForUnavailability
    allow insert using (
      exists (<default::Role>{'ProjectManager', 'RegionalDirector', 'FieldOperationsDirector'} intersect global default::currentRoles)
    );
  }
  
  scalar type Status extending enum<Active, Disabled>;
  
  scalar type Degree extending enum<
    Primary,
    Secondary,
    Associates,
    Bachelors,
    Masters,
    Doctorate
  >;
}
