using extension pg_unaccent;

module default {
  global currentActorId: uuid;
  global currentActor := (select Actor filter .id = global currentActorId);
  global currentUser := (select User filter .id = global currentActorId);
  global currentRoles := (global currentActor).roles;

  scalar type ReportPeriod extending enum<Monthly, Quarterly>;

  # Helper function to sort strings in a more natural way
  function str_sortable(value: str) -> str using (
    str_lower(
      ext::pg_unaccent::unaccent(
        str_trim(re_replace('[ [\\]|,\\-$]+', ' ', value, flags := 'g')),
      )
    )
  );

  function array_join_maybe(array: array<str>, delimiter: str) -> optional str using (
    with joined := array_join(array, delimiter)
    select if joined = "" then <str>{} else joined
  );

  scalar type nanoid extending str;
  
  scalar type RichText extending json;
  
  # A fake function to produce valid EdgeQL syntax.
  # This will be reflected to dynamically inject portable shapes in our EdgeQL queries.
  function hydrate(typeName: str, scopedValue: json) -> str
    using (typeName);
  
  # Get the inclusive upper bound of the given date range.
  function date_range_get_upper(period: range<cal::local_date>) -> cal::local_date
    using (<cal::local_date><str>assert_exists(range_get_upper(period)) - <cal::date_duration>"1 day");
}
